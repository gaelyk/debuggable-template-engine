package groovyx.gaelyk.dte

import groovyx.gaelyk.dte.DebuggableTemplateEngine;
import groovyx.gaelyk.dte.Position;
import groovyx.gaelyk.dte.TemplateParsingException;

import org.codehaus.groovy.control.messages.SyntaxErrorMessage;
import org.codehaus.groovy.syntax.SyntaxException;

import spock.lang.Specification

class DebuggableTemplateEngineSpec extends Specification {

    GroovyShell shell = new GroovyShell()
    DebuggableTemplateEngine dte = new DebuggableTemplateEngine(shell)
    
    def "Failing to parse template give much detailed information"() {
        String source = """
        Hello world!
        <% if (true) %>
        This will fail due missing opening bracket!
        <% } %>"""
        
        String script = '''out.print("""
        Hello world!
        """); if (true) ;
out.print("""
        This will fail due missing opening bracket!
        """); } ;
out.print("""""");

/* Generated by SimpleTemplateEngine */'''
        
        when:
        dte.createTemplate(source)
        
        then:
        TemplateParsingException e = thrown(TemplateParsingException)
        e.templateSource == source
        
        when:
        SyntaxException semsg = e.cause.errorCollector.errors[0].cause
        
        then:
        e.positionsMap[Position.at(semsg.startLine, semsg.startColumn)] == Position.at(3, 23)
        e.positionsMap[Position.at(semsg.endLine, semsg.endColumn)]     == Position.at(3, 23)
        e.message == '''
unexpected token: ;

1   :
2   :        Hello world!
3   :        <% if (true) %>
=   :                      ^
4   :        This will fail due missing opening bracket!
5   :        <% } %>

The template was parsed into following script:

1   :out.print("""
2   :        Hello world!
3   :        """); if (true) ;
=   :                        ^
4   :out.print("""
5   :        This will fail due missing opening bracket!
6   :        """); } ;
7   :out.print("""""");
8   :
9   :/* Generated by SimpleTemplateEngine */
'''
        
    }
    
    def "Exception thrown in template is better reported"() {
        DebuggableTemplate tmp = dte.createTemplate(
"""Blah, blah, blah
<% throw new RuntimeException('Ooops') %>
Unreachable
""")
        when:
        tmp.make().writeTo(new StringWriter())
        
        then:
        Throwable th = thrown()
        th.stackTrace.find { it.className.contains('DebuggableTemplateScript') }.lineNumber == 2
        
        
        
    }
    
    
    
}
